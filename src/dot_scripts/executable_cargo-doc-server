#!/usr/bin/env fish

function cargo-doc-server
    if ! argparse no-watch nightly cfg-docsrs p/port= root= -- $argv
        echo -n "
Usage:

  cargo-doc-server [OPTIONS]

Options:

  -p/--port <port> Port to listen on
  --nightly        Use nightly Rust toolchain
  --cfg-docsrs     Set `--cfg=docsrs` flag
  --no-watch       Stop watching and rebuilding
  --root <path>    Root directory to start the file server
"
        return 1
    end

    set port $_flag_port 5500
    set cargo_watch (set -q _flag_nightly && echo "cargo-nightly-watch" || echo "cargo-watch")
    set rustflags
    set root $_flag_root target/doc

    if set -q _flag_cfg_docsrs
        set -a rustflags --cfg docsrs
    end

    set sws_flags \
        --cors-allow-origins='*' \
        --directory-listing \
        --ignore-hidden-files \
        --host=127.0.0.1 \
        --port=$port[1] \
        --root=$root[1]

    # Start a new shell instance so that we can CTRL_C to stop the whole server.
    if set -q _flag_no_watch
        exec static-web-server $sws_flags
    else
        exec bash -c "
            $cargo_watch -E RUSTFLAGS='$rustflags' -x 'doc $argv' &
            mkdir -p target
            static-web-server $sws_flags
        "
    end
end

cargo-doc-server $argv
