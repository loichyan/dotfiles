#!/usr/bin/env fish

function main
    if test -z "$argv" || ! argparse s/src= d/dest= -- $argv
        echo -n "\
Sync Git mirror repositories

Usage:

  git-sync-mirror [OPTIONS]

Options:

  -s/--src <repo>   Source repository
  -d/--dest <repo>  Destination repository
"
        return 1
    end

    set src $_flag_src
    set dest $_flag_dest
    if test -z "$src" || test -z "$dest"
        echo "'--src/--dest' must be specified"
        return 1
    end

    pushd .
    set workdir (mktemp -d)
    cd $workdir

    git clone --mirror $src .
    # Delete PRs
    git for-each-ref --format="delete %(refname)" refs/pull | git update-ref --stdin
    git push --mirror $dest

    popd
    rm -rf $workdir
end

main $argv
